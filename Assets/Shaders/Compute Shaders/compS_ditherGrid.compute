#pragma kernel CSMain

#include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
#include "Assets/Shaders/HLSL/HelperShaderFunctions.hlsl"
#include "Assets/Shaders/HLSL/NoiseFunctions.hlsl"

RWTexture2D<float4> Result;

float _gridScale;
float _gridThickness;
float _TimeY;
float _centreLightSize;
float _sonarPingTime;
float _flareTime;
float _radialScanTime;
float _radialScanRotation;

float2 _playerPos;
float2 _flarePos;
float2 _Resolution;

Texture2D<float> _NoiseTex;

#define TAU 6.2831853
#define PIMINUSHALF 2.64159265

float2 aspectRatioPentile(float2 wh)
{
    float2 w = float2(wh.x / wh.y, 1);
    float2 h = float2(1, wh.y / wh.x);
    return max(w, h);
}

float2 rotateUV(float2 uv, float rotation)
{
    rotation *= TAU;
    float s = sin(rotation);
    float c = cos(rotation);
    float2x2 rMatrix = float2x2(c, -s, s, c);
    rMatrix *= 0.5;
    rMatrix += 0.5;
    rMatrix = rMatrix * 2 - 1;
    uv.xy = mul(uv.xy, rMatrix);
    return uv;
}

float circleSDF(float2 uv, float2 aspScreenPos)
{
    float2 p = (uv * aspScreenPos) - (_playerPos * aspScreenPos);
    float circle = 1 - length(p * _centreLightSize);
    return saturate(circle);
}

float sonarSDF(float2 uv, float2 aspScreenPos, float minValue, float maxValue)
{
    float2 p = (uv * aspScreenPos) - (_playerPos * aspScreenPos);

    p /= _sonarPingTime;
    float sonarPingLength = length(p * 0.4) * 1.5;
    float ping = saturate((1 - distance(sonarPingLength, 1 - sonarPingLength)) - _sonarPingTime);
    ping *= 1.5;
    return lerp(minValue, maxValue, ping);
}


float flareSDF(float2 uv, float2 aspScreenPos, float minValue, float maxValue)
{
    float t = sin(_flareTime * PI);
    float updatingScale = lerp(minValue, maxValue, _flareTime);

    float2 p = (uv * aspScreenPos) - (_flarePos * aspScreenPos);
    float circle = 1 - length(p * updatingScale);
    return saturate(circle * t);
}

float radialScanSDF(float2 uv, float2 aspScreenPos, float fallOff, float strength)
{
    float2 p = (uv * aspScreenPos) - (_playerPos * aspScreenPos);
    p = rotateUV(p, _radialScanRotation);
    
    float arc = atan2(p.x, p.y);
    float t = lerp(PIMINUSHALF, -PIMINUSHALF, _radialScanTime);
    float radialScanMask = pow(saturate(1 - length(arc + t)), fallOff);
    
    float logLight = saturate(dot(normalize(p), float2(0, 1)));
    float circleMask = saturate(1 - length(p));
    
    
    return logLight * circleMask * strength * radialScanMask;
}


[numthreads(8, 8, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    float2 screenPos = _Resolution;
    float2 aspScreen = aspectRatioPentile(_Resolution);
    float2 uv = (float2(id.xy) + 0.5) / screenPos;

    float2 scaledAspectRatioUV = screenPos / _gridScale;
    float2 scaledTexCoord = uv * scaledAspectRatioUV;

    float2 idCoord = round(scaledTexCoord);
    float2 gridTexCoord = idCoord / scaledAspectRatioUV;

    float2 gridSpacePlayerPos = (_playerPos * screenPos) / _gridScale;

    

    float centerLight = circleSDF(gridTexCoord, aspScreen);
    float sonarPing = sonarSDF(gridTexCoord, aspScreen, 0, 1);
    float flare = flareSDF(gridTexCoord, aspScreen, 20, 2) * 50;
    float radialScan = radialScanSDF(gridTexCoord, aspScreen, 1, 1) * 10;

    float totalMask = saturate(centerLight + sonarPing + flare + radialScan);

    [branch]
    if (totalMask < 0.05)
    {
        Result[id.xy] = float4(0, 0, 0, 0);
        return;
    }
    
    float col = 0;
    int neighbourRange = 8;
    for (int x = -neighbourRange; x <= neighbourRange; x++)
    {
        for (int y = -neighbourRange; y <= neighbourRange; y++)
        {
            float2 offset = float2(x, y);
            float4 currSC = float4(frac(scaledTexCoord - offset), floor(scaledTexCoord - offset));

            float2 localUV = (currSC.zw + 0.5) / scaledAspectRatioUV;
            float sonar = sonarSDF(localUV, aspScreen, 0, 5);
            float flare = flareSDF(localUV, aspScreen, 20, 2) * 4;
            float radialScan = radialScanSDF(localUV, aspScreen, 1, 5);
            
            float totalMask = sonar + flare + radialScan;
            
            float2 displacementDir = normalize(gridSpacePlayerPos - currSC.zw);
            float2 displacedPos = currSC.xy + offset + (displacementDir * totalMask);
            float currDistFromSquare = max(-(max(abs(displacedPos.x) - 0.5, abs(displacedPos.y) - 0.5)), 0);
            col += currDistFromSquare;
        }
    }
    
    totalMask = saturate(centerLight + sonarPing + flare + radialScan);
    
    col = smoothstep(_gridThickness, 1 - _gridThickness, col);
    col *= totalMask;
    Result[id.xy] = float4(col, totalMask, 0, 1);
}
